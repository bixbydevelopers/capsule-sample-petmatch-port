// locationType and sheddingType are included to match Alexa skill but are unused here and in the Alexa skill
// {article} from Alexa skill not needed for Bixby - NLU handles this
// {IWantType} from Alexa skill not needed for Bixby - NLU handles this
// {atTheType} from Alexa skill - appears not used by Alexa, if used likely not needed by Bixby, NLU handles this
action (MatchPet) {
  type (Search)
  description (Find a pet match)
  collect {
    input (animal) {
      type (Animal)
      min (Required)
      max (One)
      default-init {
         intent {
           value: Animal ("dog")
           goal: Animal
         }
       }
      validate {
        if (regexAllMatch(animal, 'unicorn|chimera|dragon')) {
          halt {
            dialog {
              choose (Random) {
                template ("I'm sorry, but I'm not qualified to match you with a #{value(animal)}.")
                template ("Ah yes, #{value(animal)}s are splendid creatures, but unfortunately owning one as a pet is outlawed.")
                template ("I'm sorry I can't match you with a #{value(animal)}.")
              }
            }
          }
        }
      }
    }
    input (size) {
      type (Size)
      min (Required)
      max (One)
    }
    input (temperament) {
      type (Temperament)
      min (Required)
      max (One)
    }
    input (energy) {
      type (Energy)
      min (Required)
      max (One)
    }
    input (location) {
      type (Location)
      min (Optional)
      max (One)
    }
    input (shedding) {
      type (Shedding)
      min (Optional)
      max (One)
    }
    input (comparison) {
      type (Comparison)
      min (Optional)
      max (One)
    }
    input (amount) {
      type (Amount)
      min (Optional)
      max (One)
    }
    input (units) {
      type (Units)
      min (Optional)
      max (One)
    }
  }
  output (DogBreed) {
    throws {
      error (APIError) {
        on-catch {
          halt {
            dialog {
              template ("I am really sorry. I am unable to access part of my memory. Please try again later")
            }
          }
        }
      }
    }
    on-empty {
      halt {
        dialog {
          template ("I'm sorry I couldn't find a match for a #{value(size)} #{value(temperament)} #{value(energy)} energy dog")
        }
      }
    }
  }
}